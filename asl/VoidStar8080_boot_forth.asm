	TITLE	'FIG-FORTH COLD START LOADER FOR THE (VOID *)8080 SIMULATED MICROCOMPUTER'

	.CPU	8080

	;; DURING THE SYSTEM POWER-ON START-UP CYCLE, THIS COLD START
	;; LOADER IS PLACED AT 0080H IN RAM AND STARTED. IT IS
	;; RESPONSIBLE FOR LOADING A KNOWN NUMBER OF ADDITIONAL
	;; SEQUENTIAL DATA BYTES FROM THE BDEV CONTROLLER INTO MEMORY
	;; AT A DESIGNATED LOAD ADDRESS, THEN TRANSFERRING CONTROL TO
	;; AN ENTRY POINT (INDEPENDENT OF THE LOAD ADDRESS);
	;;
	;; THIS FILE IS CONFIGURED TO LOAD VOIDSTAR8080 FORTH,
	;; WHICH IS FIG-FORTH 8080 1.1 WITH CUSTOMIZATION.

	ORG	0080H		;THIS LOADER RUNS AT 0080H

BDDAT	EQU	14		;DISK CONTROLLER DATA PORT

LOADER:
	LXI	SP,STACK
	POP	D		;GET TRANSFER SIZE IN BYTES
	POP	H		;GET MEMORY LOAD ADDRESS
-:	;; byte transfer loop
	IN	BDDAT		;GET BYTE FROM DISK
	MOV	M,A		;PUT BYTE TO MEMORY
	INX	H
	DCR	E
	JNZ	-
	DCR	D
	JNZ	-
	RET			;TURN CONTROL OVER TO FORTH

	;; === === === === === === === === === === === ===
	;; FORTH ADDRESS CONFIGURATION

TEXT	EQU	0100H		;FIG-FORTH LOAD ADDRESS
ENTRY	EQU	0100H		;FIG-FORTH COLD ENTRY POINT
ETEXT	EQU	1C00H		;FIG-FORTH END OF DISK IMAGE
BYTES	EQU	ETEXT-TEXT	;SIZE OF FIG-FORTH IMAGE TO LOAD, IN BYTES

STACK	EQU	LOADER+0080H-2*3

AVAIL   EQU     STACK-$         ;loader could grow by this much.

	;; === === === === === === === === === === === ===
	;; COLD START CONFIGURATION TABLE

	ORG	STACK
	DW	BYTES		;PATCH HERE TO CHANGE IMAGE SIZE IN BYTES
	DW	TEXT		;PATCH HERE TO CHANGE IMAGE LOAD ADDRESS
	DW	ENTRY		;PATCH HERE TO CHANGE IMAGE ENTRY POINT

	END
