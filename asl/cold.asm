        TITLE   'cold start loader'

        .CPU    8080

        ORG     00000H          ;BASE OF MEMORY
MSIZE   EQU     16              ;MEMORY SIZE IN KILOBYTES
BIAS    EQU     (MSIZE-16)*1024 ;BIAS TO ADD TO LOAD ADDRESSES
LOADP   EQU     2900H           ;LOAD POINT FOR CP/M SYSTEM
BIOS    EQU     3E00H           ;BASIC I/O SYSTEM (2 PAGES = 512 BYTES)
BOOT    EQU     BIOS            ;COLD START ENTRY POINT IN BIOS
SIZE    EQU     BIOS+512-LOADP  ;SIZE OF THE CP/M SYSTEM TO LOAD
SECTS   EQU     SIZE/128        ;NUMBER OF SECTORS TO LOAD
;
; BEGIN THE LOAD OPERATIONS
COLD:   LXI	B,2             ;CLEAR B TO 0, SET C TO SECTOR 2
        MVI     D,SECTS         ;NUMBER OF SECTORS TO LOAD IS IN D
        LXI     H,LOADP+BIAS    ;LOAD POINT IN H,L
;
LSECT:  ;LOAD NEXT SECTOR
; INSERT INLINE CODE AT THIS POINT TO READ ONE 128 BYTE SECTOR
; FROM TRACK GIVEN BY REGISTER B,
;     SECTOR GIVEN BY REGISTER C,
; INTO ADDRESS GIVEN BY REGISTER PAIR H,L
; BRANCH TO LOCATION 'COLD' IF A READ ERROR OCCURS
;
; ****************************************************************
; USER SUPPLIED READ OPERATION GOES HERE
; ****************************************************************
; (SPACE IS RESERVED FOR YOUR PATCH)
	JMP	PASTPATCH	;REMOVE THIS JUMP WHEN PATCHED
        DS      60H
;
PASTPATCH:
; GO TO NEXT SECTOR IF LOAD IS INCOMPLETE
	DCR	D		;SECTS=SECTS-1
        JZ      BOOT+BIAS       ;GO TO BOOT LOADER AT 3E00H+BIAS
;
; MORE SECTORS TO LOAD
; USE SP FOR SCRATCH REGISTER TO HOLD LOAD ADDRESS INCREMENT
	LXI	SP,128
        DAD     SP              ;HL=HL+128 TO NEXT LOAD ADDRESS
        INR     C               ;SECTOR=SECTOR+1
        MOV     A,C             ;MOVE SECTOR COUNT TO A FOR COMPARE
        CPI     27              ;END OF CURRENT TRACK?
        JC      LSECT           ;CARRY GENERATED IF SECTOR < 27
;
; END OF TRACK, MOVE TO NEXT TRACK
	MVI	C,1		;SECTOR=1
        INR     B               ;TRACK=TRACK+1
        JMP     LSECT           ;FOR ANOTHER SECTOR
;
        END
